apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "org.opencv.template"
        minSdkVersion 15
        targetSdkVersion 22
    }

    sourceSets.main {
        jni.srcDirs = []
        jniLibs.srcDir 'src/main/libs'
    }

    task buildNative(type: Exec, description: 'Compile JNI source via NDK') {
        def ndkDir = android.ndkDirectory

        // reading 'local.properties' to get openCV sdk directory path.
        // you must add line 'opencv.sdk.dir=<Your OpenCV SDK Directory>' to 'local.properties'.
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        def openCVSdkDir = properties.getProperty('opencv.sdk.dir')

        commandLine "$ndkDir/ndk-build",
                "OPENCV_ANDROID_SDK=$openCVSdkDir",
                '-C', file('src/main/jni').absolutePath,
                '-j', Runtime.runtime.availableProcessors(),
                'all'

        doLast {
            copy {
                from 'src/main/opencvLib/armeabi-v7a/libopencv_java3.so'
                into 'src/main/libs/armeabi-v7a'
            }
        }
    }

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn buildNative
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

dependencies {
    compile project(':openCVLibrary310')

    compile 'io.reactivex:rxandroid:1.2.0'
    compile 'io.reactivex:rxjava:1.1.5'
    compile 'com.squareup.okhttp3:okhttp:3.2.0'
}
